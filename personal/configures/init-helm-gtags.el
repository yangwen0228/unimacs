;;; init-helm-gtags.el --- configure for helm gtags
;;; Commentary:
;; comments

;;; Code:
(use-package helm-gtags
  :init
  (helm-gtags-mode t)
  :config
  (setq helm-gtags-update-interval-second nil)
  (setq helm-gtags-path-style 'root)
  (setq helm-gtags-ignore-case nil)
  (setq helm-gtags-read-only nil)
  (setq helm-gtags-auto-update t)
  (setq helm-gtags-pulse-at-cursor t)
  (setq helm-gtags-cache-select-result t)
  (setq helm-gtags-cache-max-result-size (* 10 1024 1024))

  (bind-keys* ("M-t"     . helm-gtags-find-tag)
              ("M-r"     . helm-gtags-find-rtag-adapter)
              ("C-c M-s" . helm-gtags-find-symbol)
              ("C-c M-f" . helm-gtags-find-files)
              ("C-c M-g" . helm-gtags-find-pattern)
              ("C-t"     . helm-gtags-pop-stack)
              ("C-c <"   . helm-gtags-previous-history)
              ("C-c >"   . helm-gtags-next-history)
              ("C-c s s" . helm-gtags-show-stack))

  (setq helm-gtags-path-style  'relative
        helm-gtags-ignore-case t
        helm-gtags-read-only   t)

  (defun helm-gtags-find-rtag-for-ctags (tag)
    "TODO: Workaround for ctags rtags: Use TAG to find all matches, then filter procs."
    (helm-gtags--common '(helm-source-gtags-pattern) (format "\\<%s\\>" tag )))

  (defun helm-gtags-find-rtag-adapter (tag)
    "Workaround for tcl rtags: TAG: choose function according to major mode."
    (interactive
     (list (helm-gtags--read-tagname 'tag)))
    (if (member major-mode '(c-mode c++-mode objc-mode java-mode))
        (helm-gtags-find-rtag tag)
      (helm-gtags-find-rtag-for-ctags tag)))

  (defadvice helm-gtags-find-pattern (before helm-gtags-find-pattern activate)
    "Ignore case when use pattern to search."
    (setq helm-gtags-ignore-case t))
  (defadvice helm-gtags-find-pattern (after helm-gtags-find-pattern activate)
    "Don't ignore case when find definition and reference."
    (setq helm-gtags-ignore-case nil))

  (setq helm-gtags-default-label "default")
  (add-hook 'prog-mode-hook
            (lambda ()
              (set (make-local-variable 'helm-gtags-default-label)
                   (if (member major-mode
                               '(c-mode c++-mode objc-mode java-mode))
                       "default"
                     "ctags"))))

  (defun helm-gtags--update-tags-command (how-to)
    "Override the original function."
    (cl-case how-to
      (entire-update `("global" ,(concat "--gtagslabel=" helm-gtags-default-label) "-u"))
      (generate-other-directory (list "global" (concat "--gtagslabel=" helm-gtags-default-label) (helm-gtags--read-tag-directory)))
      (single-update (list "global" (concat "--gtagslabel=" helm-gtags-default-label) "--single-update" (helm-gtags--real-file-name)))))

  (defun helm-gtags-delete-tags ()
    "Delete file GTAGS, GRTAGS, GPATH, ID etc. generated by gtags."
    (interactive)
    (let* ((root-dir (helm-gtags--tag-directory))
           (re (concat "\\`" (regexp-opt '("GPATH" "GRTAGS" "GTAGS" "ID")) "\\'"))
           (files (cl-remove-if-not
                   (lambda (file)
                     ;; Don't trust `directory-files'.
                     (let ((case-fold-search nil))
                       (string-match-p re (file-name-nondirectory file))))
                   (directory-files root-dir t re)))
           (buffer "*GTags File List*"))
      (or files (user-error "No tag files found"))
      (with-output-to-temp-buffer buffer
        (princ (mapconcat #'identity files "\n")))

      (let ((win (get-buffer-window buffer)))
        (unwind-protect
            (progn
              (fit-window-to-buffer win)
              (when (yes-or-no-p "Remove GNU Global tag files? ")
                (with-demoted-errors (mapc #'delete-file files))
                ))
          (when (window-live-p win)
            (quit-window t win))))))

  (defun helm-gtags-edit ()
    (interactive)
    (helm-exit-and-execute-action 'helm-gtags--edit))

  (defun helm-gtags--edit (_candidate)
    (let ((default-directory (helm-gtags--base-directory)))
      (with-current-buffer (get-buffer-create "*helm-gtags-edit*")
        (erase-buffer)
        (let (buf-content)
          (with-current-buffer (get-buffer "*helm gtags*")
            (goto-char (point-min))
            (forward-line 1)
            (let* ((body-start (point))
                   (marked-lines (cl-loop for ov in (overlays-in body-start (point-max))
                                          when (eq 'helm-visible-mark (overlay-get ov 'face))
                                          return (helm-marked-candidates))))
              (if (not marked-lines)
                  (setq buf-content (buffer-substring-no-properties
                                     body-start (point-max)))
                (setq buf-content (concat (mapconcat 'identity marked-lines "\n") "\n")))))
          (insert buf-content)
          (add-text-properties (point-min) (point-max)
                               '(read-only t rear-nonsticky t front-sticky t))
          (let ((inhibit-read-only t))
            (setq header-line-format
                  (format "[%s] C-x C-s: Save, C-c C-g: Cancel"
                          (abbreviate-file-name default-directory)))
            (goto-char (point-min))
            (while (re-search-forward "^\\(\\(?:[^:]+:\\)\\{1,2\\}\\)\\(.*\\)$" nil t)
              (let ((file-line-begin (match-beginning 1))
                    (file-line-end (match-end 1))
                    (body-begin (match-beginning 2))
                    (body-end (match-end 2)))
                (add-text-properties file-line-begin file-line-end
                                     '(face font-lock-function-name-face
                                            intangible t))
                (remove-text-properties body-begin body-end '(read-only t))
                (set-text-properties body-end (1+ body-end)
                                     '(read-only t rear-nonsticky t))))))))
    (other-window 1)
    (switch-to-buffer (get-buffer "*helm-gtags-edit*"))
    (goto-char (point-min))
    (setq next-error-function 'compilation-next-error-function)
    (setq-local compilation-locs (make-hash-table :test 'equal :weakness 'value))
    (use-local-map helm-gtags-edit-map))

  (defun helm-gtags--edit-commit ()
    (interactive)
    (goto-char (point-min))
    (let ((read-only-files 0)
          (default-directory (helm-gtags--base-directory))
          (line-deletes (make-hash-table :test #'equal)))
      (while (re-search-forward "^\\([^:]+\\):\\([1-9][0-9]*\\):\\(.*\\)$" nil t)
        (let* ((file (match-string-no-properties 1))
               (line (string-to-number (match-string-no-properties 2)))
               (body (match-string-no-properties 3))
               (ovs (overlays-at (line-beginning-position))))
          (with-current-buffer (find-file-noselect file)
            (if buffer-read-only
                (cl-incf read-only-files)
              (goto-char (point-min))
              (let ((deleted-lines (gethash file line-deletes 0))
                    (deleted (and ovs (overlay-get (car ovs) 'helm-gtags-deleted))))
                (forward-line (- line 1 deleted-lines))
                (delete-region (line-beginning-position) (line-end-position))
                (if (not deleted)
                    (insert body)
                  (let ((beg (point)))
                    (forward-line 1)
                    (delete-region beg (point))
                    (puthash file (1+ deleted-lines) line-deletes)))
                (save-buffer))))))
      ;; TODO
      ;; (select-window helm-gtags--original-window)
      (kill-buffer (get-buffer "*helm-gatgs-edit*"))
      (if (not (zerop read-only-files))
          (message "%d files are read-only and not editable." read-only-files)
        (message "Success update"))))

  (defun helm-gtags--edit-abort ()
    (interactive)
    (when (y-or-n-p "Discard changes ?")
      ;; TODO
      ;; (select-window helm-gtags--original-window)
      (kill-buffer (get-buffer "*helm-gatgs-edit*"))
      (message "Abort edit")))

  (defface helm-gtags-edit-deleted-line
    '((t (:inherit font-lock-comment-face :strike-through t)))
    "Face of deleted line in edit mode."
    :group 'helm-gtags)

  (defun helm-gtags--mark-line-deleted ()
    (interactive)
    (let* ((beg (line-beginning-position))
           (end (line-end-position))
           (ov (make-overlay beg end)))
      (overlay-put ov 'face 'helm-gtags-edit-deleted-line)
      (overlay-put ov 'helm-gtags-deleted t)))

  (defun helm-gtags--unmark ()
    (interactive)
    (dolist (ov (overlays-in (line-beginning-position) (line-end-position)))
      (when (overlay-get ov 'helm-gtags-deleted)
        (delete-overlay ov))))

  (defun helm-gtags--common (srcs tagname)
    (helm-gtags--clear-variables)
    (let ((helm-quit-if-no-candidate t)
          (helm-execute-action-at-once-if-one t)
          (dir (helm-gtags--searched-directory))
          (src (car srcs))
          (preselect-regexp (when helm-gtags-preselect
                              (regexp-quote (helm-gtags--current-file-and-line)))))
      (when (symbolp src)
        (setq src (symbol-value src)))
      (unless helm-gtags--use-otherwin
        (setq helm-gtags--use-otherwin (helm-gtags--using-other-window-p)))
      (when tagname
        (setq helm-gtags--query tagname))
      (let ((tagroot (helm-gtags--find-tag-simple)))
        (helm-attrset 'helm-gtags-base-directory dir src)
        (helm-attrset 'name (concat "GNU Global at " (or dir tagroot)) src)
        (helm :sources srcs :buffer helm-gtags--buffer
              :preselect preselect-regexp
              :keymap helm-gtags-mode-map))))

  (setq helm-gtags-mode-map
        (let ((map (make-sparse-keymap)))
          (set-keymap-parent map helm-map)
          (define-key map (kbd "C-c C-e") 'helm-gtags-edit)
          map))
  ;; (define-key helm-gtags-mode-map (kbd "C-c C-e") 'helm-gtags-edit)

  (setq helm-gtags-edit-map
        (let ((map (make-sparse-keymap)))
          (define-key map (kbd "C-x C-s") 'helm-gtags--edit-commit)
          (define-key map (kbd "C-c C-g") 'helm-gtags--edit-abort)
          (define-key map (kbd "C-c C-d") 'helm-gtags--mark-line-deleted)
          (define-key map (kbd "C-c C-u") 'helm-gtags--unmark)
          map))

  :commands (helm-gtags-create-tags helm-gtags-update-tags helm-gtags-delete-tags)
  )

(provide 'init-helm-gtags)
;;; init-helm-gtags.el ends here